?start: program

program: statement* -> prgm

?statement: "let" CNAME ":" TYPE "=" expression ";"                         -> asgn
          | "print" expression ";"                                          -> prnt
          | "if" expression block ("elif" expression block)* ["else" block] -> ifel

block: "{" statement* "}" -> blck

?expression: term
           | expression "+" term -> addn
           | expression "-" term -> subn

?term: factor
     | term "*" factor -> muln
     | term "/" factor -> divn

?factor: NUMBER -> numb
       | CNAME
       | "(" expression ")"

TYPE: "i32"

%import common.NUMBER
%import common.WS
%import common.CNAME

%ignore WS
