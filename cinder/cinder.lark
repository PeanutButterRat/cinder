?start: program

program: statement* -> prgm

?statement: "let" CNAME ":" TYPE "=" value ";"                    -> asgn
          | "print" value ";"                                     -> prnt
          | "if" value block ("elif" value block)* ["else" block] -> ifel

block: "{" statement* "}" -> blck

?value: logical

?logical: logical "and" comparison -> andx
       | logical "or" comparison   -> orxp
       | "not" logical             -> notx
       | comparison

?comparison: expression
           | comparison ">" expression  -> grth
           | comparison ">=" expression -> greq
           | comparison "<" expression  -> lsth
           | comparison "<=" expression -> lseq
           | comparison "!=" expression -> nteq
           | comparison "==" expression -> equl

?expression: term
           | expression "+" term -> addn
           | expression "-" term -> subn

?term: factor
     | term "*" factor -> muln
     | term "/" factor -> divn

?factor: NUMBER  -> numb
       | BOOL    -> bool
       | CNAME
       | "(" logical ")"

TYPE: "i32" | "bool"
BOOL: "true" | "false"

%import common.NUMBER
%import common.WS
%import common.CNAME

%ignore WS
