?start: program

program: function* -> program

function: "fn" CNAME "(" (parameter ("," parameter)*)? ")" ["->" TYPE] block
parameter: CNAME ":" TYPE
call: CNAME "(" (expression ("," expression)*)? ")"

?statement: "let" CNAME ":" TYPE "=" expression ";"                         -> assign
          | "print" expression ";"                                          -> print
          | "if" expression block ("elif" expression block)* ["else" block] -> if_else
          | call ";"
          | "return" expression ";"                                         -> return

block: "{" statement* "}" -> block

?expression: logical

?logical: logical "and" comparison -> and
       | logical "or" comparison   -> or
       | "not" logical             -> not
       | comparison

?comparison: arithmetic
           | comparison ">" arithmetic  -> greater_than
           | comparison ">=" arithmetic -> greater_equal
           | comparison "<" arithmetic  -> less_than
           | comparison "<=" arithmetic -> less_equal
           | comparison "!=" arithmetic -> not_equal
           | comparison "==" arithmetic -> equal

?arithmetic: term
           | arithmetic "+" term -> addition
           | arithmetic "-" term -> subtraction

?term: factor
     | term "*" factor -> multiplication
     | term "/" factor -> division

?factor: NUMBER  -> number
       | BOOL    -> boolean
       | CNAME
       | "(" logical ")"
       | call

TYPE: "i32" | "bool"
BOOL: "true" | "false"

%import common.NUMBER
%import common.WS
%import common.CNAME
%import common.CPP_COMMENT

%ignore WS
%ignore CPP_COMMENT
