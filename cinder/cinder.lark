?start: program

program: statement* -> prgm

?statement: "let" CNAME ":" TYPE "=" value ";"                    -> asgn
          | "print" value ";"                                     -> prnt
          | "if" value block ("elif" value block)* ["else" block] -> ifel

block: "{" statement* "}" -> blck

?value: comparison

?comparison: expression
           | expression ">" expression  -> grth
           | expression ">=" expression -> greq
           | expression "<" expression  -> lsth
           | expression "<=" expression -> lseq
           | expression "!=" expression -> nteq
           | expression "==" expression -> equl

?expression: term
           | expression "+" term -> addn
           | expression "-" term -> subn

?term: factor
     | term "*" factor -> muln
     | term "/" factor -> divn

?factor: NUMBER  -> numb
       | LITERAL -> bool
       | CNAME
       | "(" expression ")"

TYPE: "i32" | "bool"
LITERAL: "true" | "false"

%import common.NUMBER
%import common.WS
%import common.CNAME

%ignore WS
