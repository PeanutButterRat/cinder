?start: program

program: function* -> program

function: "fn" CNAME "()" block
call: CNAME "()"

?statement: "let" CNAME ":" TYPE "=" value ";"                    -> assign
          | "print" value ";"                                     -> print
          | "if" value block ("elif" value block)* ["else" block] -> if_else
          | call ";"

block: "{" statement* "}" -> block

?value: logical

?logical: logical "and" comparison -> and
       | logical "or" comparison   -> or
       | "not" logical             -> not
       | comparison

?comparison: expression
           | comparison ">" expression  -> greater_than
           | comparison ">=" expression -> greater_equal
           | comparison "<" expression  -> less_than
           | comparison "<=" expression -> less_equal
           | comparison "!=" expression -> not_equal
           | comparison "==" expression -> equal

?expression: term
           | expression "+" term -> addition
           | expression "-" term -> subtraction

?term: factor
     | term "*" factor -> multiplication
     | term "/" factor -> division

?factor: NUMBER  -> number
       | BOOL    -> boolean
       | CNAME
       | "(" logical ")"
       | call

TYPE: "i32" | "bool"
BOOL: "true" | "false"

%import common.NUMBER
%import common.WS
%import common.CNAME

%ignore WS
